/*
 * Copyright 2018 Thibault Seisel
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId 'fr.nihilus.music'
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.appVersionCode
        versionName rootProject.appVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    // Retrieve keystore properties from this machine's global Gradle properties.
    if (project.hasProperty('android.signing.keyAlias')
            && project.hasProperty('android.signing.keyPassword')
            && project.hasProperty('android.signing.storeFile')
            && project.hasProperty('android.signing.storePassword')) {

        signingConfigs {
            config {
                keyAlias project.property('android.signing.keyAlias')
                keyPassword project.property('android.signing.keyPassword')
                storeFile file(project.property('android.signing.storeFile'))
                storePassword project.property('android.signing.storePassword')
            }
        }
    } else {
        project.logger.warn('''
            This machine does not define a signing configuration, 
            and therefore cannot build releases. You have to define the following properties:
            * android.signing.keyAlias
            * android.signing.keyPassword
            * android.signing.storeFile
            * android.signing.storePassword
            in the local ~.gradle/gradle.properties file of your machine.
            '''.stripIndent())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Configure Kotlin compiler optimisations for releases
            kotlinOptions {
                freeCompilerArgs = [
                        'Xno-param-assertions',
                        'Xno-call-assertions',
                        'Xno-receiver-assertions'
                ]
            }
        }

        // Allow installing a debug version of the application along a production one
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            debuggable true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

androidExtensions {
    experimental = true
}

kapt {
    arguments {
        arg('dagger.formatGeneratedSource', 'disabled')
        arg('dagger.fastInit', 'enabled')
    }
}

dependencies {
    // Sub-project containing the Media Service.
    api project(':media')

    // Kotlin language support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"

    // Support library dependencies
    implementation "com.android.support:support-v4:$rootProject.supportLibVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:palette-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:design:$rootProject.supportLibVersion"
    implementation "com.android.support:preference-v7:$rootProject.supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintVersion"

    // Android Arch Components
    implementation "android.arch.lifecycle:extensions:$rootProject.lifecycleVersion"
    implementation "android.arch.lifecycle:common-java8:$rootProject.lifecycleVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    // My libraries
    implementation "fr.nihilus:recyclerfragment:$rootProject.recyclerVersion"
    implementation 'com.github.thibseisel:ratioimageview:1.0.0'

    // Dagger
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    // ReactiveX
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"

    // Timber Logging
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // Test dependencies
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectricVersion"
    testImplementation "com.google.truth:truth:$rootProject.truthVersion"

    androidTestImplementation "com.android.support:support-annotations:$rootProject.supportLibVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.androidTestVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.androidTestVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoVersion"
}