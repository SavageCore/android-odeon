/*
 * Copyright 2020 Thibault Seisel
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
import com.android.build.gradle.BaseExtension

buildscript {
    apply from: 'gradle/dependencies.gradle'

    ext {
        // SDK and tools
        compileSdkVersion = 29
        minSdkVersion = 21
        targetSdkVersion = 29
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${versions.androidx.navigation}"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.33.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate {
        // Common Android configuration for all modules.
        (extensions.findByName('android') as BaseExtension)?.with {

            it.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            it.kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8
                freeCompilerArgs += [
                        '-Xopt-in=kotlin.RequiresOptIn',
                        '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                ]
            }

            it.testOptions {
                // Include Android resources in unit tests to be resolved by Robolectric.
                unitTests.includeAndroidResources = true
            }
        }

        // Configure Dagger code generation for all modules.
        extensions.findByName('kapt')?.arguments {
            arg('dagger.formatGeneratedSource', 'disabled')
            arg('dagger.fastInit', 'enabled')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.withType(Wrapper) {
    distributionType = Wrapper.DistributionType.ALL
}

/**
 * Retrieve a Gradle property value, or return the provided default value if it is not defined.
 *
 * @param propertyName The name of the property to find.
 * @param defaultValue The value to use when the requested property is not defined.
 * @return The value of the Gradle property.
 */
def <T> T propOrDefault(String propertyName, T defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue ?: defaultValue
}

/**
 * Resolve the path of a file relatively to the root project.
 * This is meant to be used by sub-projects to reference a file from the root project.
 *
 * @param filename The path of the file whose path is to be resolved.
 * @return The path of the specified file.
 */
String rootProjectFile(String filename) {
    def file = project.file(filename)
    if (!file.exists()) {
        throw new FileNotFoundException("No such file: ${file.path}")
    }

    return file.path
}